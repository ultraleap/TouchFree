; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define ScServiceVersion "1.0.0-alpha5"
#define ScServicePublisher "Ultraleap Inc."
#define SCServiceProdName "ScreenControl Service"
#define ScServiceURL "https://ultraleap.com"
#define ServiceUIName "ScreenControl Configuration"
#define ServiceUIExeName "ScreenControlServiceUI.exe"
#define TrayAppExeName "SC_ServiceUITray.exe"
#define WrapperExeName "SC_ServiceWrapper.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{1CFAE885-39F8-431E-AD0F-C623B39F71E4}
AppName={#SCServiceProdName}
AppVersion={#ScServiceVersion}
AppVerName={#SCServiceProdName} {#ScServiceVersion}
AppPublisher={#ScServicePublisher}
AppPublisherURL={#ScServiceURL}
AppSupportURL={#ScServiceURL}
AppUpdatesURL={#ScServiceURL}
CreateUninstallRegKey=yes
DefaultDirName={autopf}\{#SCServiceProdName}
DisableProgramGroupPage=yes
LicenseFile={#SourcePath}..\..\LICENSE
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir="{#SourcePath}..\..\Installer_Build"
OutputBaseFilename=ScreenControl_Service_1.0.0_alpha5_Installer
Compression=lzma
SolidCompression=yes
VersionInfoCompany={#ScServicePublisher}
VersionInfoProductName={#ScServiceProdName}
UpdateUninstallLogAppName=no
VersionInfoVersion=1.0.0.4
WizardStyle=modern
PrivilegesRequired=admin

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "{#SourcePath}..\..\Service_Package\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#SCServiceProdName}"; Filename: "{app}\Tray\{#TrayAppExeName}";
Name: "{autostartup}\{#SCServiceProdName}"; Filename: "{app}\Tray\{#TrayAppExeName}";

[Registry]
Root: HKA64; Subkey: "Software\Ultraleap"; Flags: uninsdeletekeyifempty
Root: HKA64; Subkey: "Software\Ultraleap\ScreenControl"; Flags: uninsdeletekeyifempty
Root: HKA64; Subkey: "Software\Ultraleap\ScreenControl\Service"; Flags: uninsdeletekey
Root: HKA64; Subkey: "Software\Ultraleap\ScreenControl\Service\Settings"; ValueType: string; ValueName: "WrapperExePath"; ValueData: "{app}\Wrapper\{#WrapperExeName}"

[Run]
Filename: "{app}\ServiceUI\{#ServiceUIExeName}"; Description: "{cm:LaunchProgram,{#StringChange(ServiceUIName, '&', '&&')}}"; Flags: runascurrentuser nowait postinstall skipifsilent
Filename: "{app}\Tray\{#TrayAppExeName}"; Flags: runhidden nowait;
Filename: "{app}\Wrapper\{#WrapperExeName}"; Parameters: "install"; Flags: runhidden
Filename: "net.exe"; Parameters: "start ""ScreenControl Service"""; Flags: runhidden

[UninstallRun]
Filename: "{cmd}"; Parameters: "/C taskkill /im SC_ServiceUITray.exe /f /t"; RunOnceId: "StopTrayIconApp"; Flags: runhidden
Filename: "net.exe"; Parameters: "stop ""ScreenControl Service"""; RunOnceId: "StopService"; Flags: runhidden
Filename: "{app}\Wrapper\{#WrapperExeName}"; Parameters: "uninstall"; RunOnceId: "UninstallService"; Flags: runhidden

[Code]
function GetWrapperPath: string;
var
  wrapperExePath: string;
  wrapperRegistryPath: String;
begin
  Result := '';
  wrapperRegistryPath := ExpandConstant('Software\Ultraleap\ScreenControl\Service\Settings');
  wrapperExePath := '';
  if not RegQueryStringValue(HKLM64, wrapperRegistryPath, 'WrapperExePath', wrapperExePath) then
    RegQueryStringValue(HKCU64, wrapperRegistryPath, 'WrapperExePath', wrapperExePath);
  Result := wrapperExePath;
end;

function PrepareToInstall(var NeedsRestart: Boolean): String;
var
  ResultCode: integer;
  WrapperPath: string;
begin
  WrapperPath := GetWrapperPath();

  Log(WrapperPath);

  if CompareText(WrapperPath, '') > 0 then
  begin
    Exec('cmd', '/C taskkill /im SC_ServiceUITray.exe /f /t', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
    Exec('net', 'stop "ScreenControl Service"', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
    Exec(ExpandConstant(WrapperPath), 'uninstall', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
  end;

  // Proceed Setup
  Result := '';
end;
